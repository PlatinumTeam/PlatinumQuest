//-----------------------------------------------------------------------------
// Copyright (c) 2021 The Platinum Team
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to
// deal in the Software without restriction, including without limitation the
// rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
// sell copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
// FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
// DEALINGS IN THE SOFTWARE.
//-----------------------------------------------------------------------------

//--- OBJECT WRITE BEGIN ---
new GuiControl(ExitGameDlg) {
	profile = "GuiDefaultProfile";
	horizSizing = "width";
	vertSizing = "height";
	position = "0 0";
	extent = "1024 768";
	minExtent = "8 8";
	visible = "1";
	helpTag = "0";
	wrap = "0";
		_guiSize = "1024 768";
		commandSelect = "hideControllerUI(); ExitGameDlg.close(); ExitGameDlg.end();";
		commandCancel = "hideControllerUI(); ExitGameDlg.close(); resumeGame();";
		commandAlt1 = "hideControllerUI(); ExitGameDlg.close(); resumeGame(); restartLevel(true);";
		commandLT = "OptionsGui.show(\"exit\");";
		commandRT = "RootGui.pushDialog(JukeboxDlg);";
		commandNameSelect = "Exit";
		commandNameCancel = "Resume";
		commandNameAlt1 = "Restart";
		commandNameLT = "Options";
		commandNameRT = "Jukebox";

	new GuiBitmapCtrl() {
		profile = "GuiDefaultProfile";
		horizSizing = "width";
		vertSizing = "height";
		position = "0 0";
		extent = "1024 768";
		minExtent = "8 8";
		visible = "1";
		helpTag = "0";
		bitmap = "./exit/black";
		wrap = "0";
	};
	new GuiControl() {
		profile = "GuiDefaultProfile";
		horizSizing = "width";
		vertSizing = "center";
		position = "0 0";
		extent = "1024 600";
		minExtent = "8 8";
		visible = "1";
		helpTag = "0";

		new GuiControl() {
			profile = "GuiDefaultProfile";
			horizSizing = "width";
			vertSizing = "bottom";
			position = "400 46";
			extent = "800 264";
			minExtent = "8 8";
			visible = "1";
			helpTag = "0";

			new GuiBitmapCtrl() {
				profile = "GuiDefaultProfile";
				horizSizing = "center";
				vertSizing = "height";
				position = "100 0";
				extent = "600 264";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				bitmap = "./loading/loading_main_text";
				wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoTitle) {
				profile = "GuiMLTextProfile";
				horizSizing = "center";
				vertSizing = "bottom";
				position = "150 0"; // "center" with horizontal position does nothing. This is actually at 238.
				extent = "500 24";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
				wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoLeftmostChallengeTitle) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "28 51"; // 28 (40 - 12). values 28 168 238 308 448
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoLeftcenterChallengeTitle) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "168 51"; // 28 + 140
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoCenterChallengeTitle) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "238 51"; // 50% of 500, then -12
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoRightcenterChallengeTitle) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "308 51"; // 28 + 140 + 140
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoRightmostChallengeTitle) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "448 51"; // 28 + 140 + 140 + 140
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoLeftmostChallengeTime) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "28 71";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoLeftcenterChallengeTime) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "168 71";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoCenterChallengeTime) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "238 71";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoRightcenterChallengeTime) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "308 71";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoRightmostChallengeTime) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "448 71";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoPersonalBestTitle) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "238 106";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoPersonalBestTime) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "238 126";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};


			new GuiMLTextCtrl(ExitGameInfoPersonalBestModeTitle) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "28 106";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoPersonalBestModeTime) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "28 126";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoRecordModeTitle) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "448 106";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
			new GuiMLTextCtrl(ExitGameInfoRecordModeTime) {
				profile = "GuiMLTextProfile";
				horizSizing = "left";
				vertSizing = "bottom";
				position = "448 126";
				extent = "150 32";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};

			new GuiMLTextCtrl(ExitGameInfo) {
				profile = "GuiMLTextProfile";
				horizSizing = "center";
				vertSizing = "bottom";
				position = "200 100";
				extent = "500 50";
				minExtent = "8 8";
				visible = "1";
				helpTag = "0";
				lineSpacing = "2";
				allowColorChars = "0";
				maxChars = "-1";
					wrap = "0";
			};
		};
		new GuiBitmapButtonTextCtrl(ExitGameYes) {
			profile = "MenuButtonProfile";
			horizSizing = "right";
			vertSizing = "bottom";
			position = "75 226";
			extent = "500 84";
			minExtent = "8 8";
			visible = "1";
			command = "ExitGameDlg.close(); ExitGameDlg.end();";
			accelerator = "return";
			helpTag = "0";
			text = "           Exit";
			groupNum = "-1";
			buttonType = "PushButton";
			repeatPeriod = "1000";
			repeatDecay = "1";
			bitmap = "./menu/menu";
		};
		new GuiBitmapButtonTextCtrl(ExitGameNo) {
			profile = "MenuButtonProfile";
			horizSizing = "right";
			vertSizing = "bottom";
			position = "75 46";
			extent = "500 84";
			minExtent = "8 8";
			visible = "1";
			command = "hideControllerUI(); ExitGameDlg.close(); resumeGame();";
			accelerator = "escape";
			helpTag = "0";
			text = "           Resume";
			groupNum = "-1";
			buttonType = "PushButton";
			repeatPeriod = "1000";
			repeatDecay = "1";
			bitmap = "./menu/menu";
		};
		new GuiBitmapButtonTextCtrl(ExitGameRestart) {
			profile = "MenuButtonProfile";
			horizSizing = "right";
			vertSizing = "bottom";
			position = "75 136";
			extent = "500 84";
			minExtent = "8 8";
			visible = "1";
			command = "hideControllerUI(); ExitGameDlg.close(); resumeGame(); restartLevel(true);";
			helpTag = "0";
			text = "           Restart";
			groupNum = "-1";
			buttonType = "PushButton";
			repeatPeriod = "1000";
			repeatDecay = "1";
			bitmap = "./menu/menu";
		};
		new GuiBitmapButtonTextCtrl(ExitGameOptions) {
			profile = "MenuButtonProfile";
			horizSizing = "right";
			vertSizing = "bottom";
			position = "75 406";
			extent = "500 84";
			minExtent = "8 8";
			visible = "1";
			command = "OptionsGui.show(\"exit\");";
			helpTag = "0";
			text = "           Options";
			groupNum = "-1";
			buttonType = "PushButton";
			repeatPeriod = "1000";
			repeatDecay = "1";
			bitmap = "./menu/menu";
		};
		new GuiBitmapButtonTextCtrl(ExitGameJukebox) {
			profile = "MenuButtonProfile";
			horizSizing = "right";
			vertSizing = "bottom";
			position = "75 496";
			extent = "500 84";
			minExtent = "8 8";
			visible = "1";
			command = "RootGui.pushDialog(JukeboxDlg);";
			helpTag = "0";
			text = "           Music";
			groupNum = "-1";
			buttonType = "PushButton";
			repeatPeriod = "1000";
			repeatDecay = "1";
			bitmap = "./menu/menu";
		};
	};
};
//--- OBJECT WRITE END ---

function ExitGameDlg::end() {
	//If we're waiting to end a recording, do that before anything else
	if (recordEnd("ExitGameDlg.end();")) {
		return;
	}

	if ($Game::Introduction) {
		resumeGame();
		menuOnPostIntroduction();
	} else {
		menuMissionExit();
	}
}

function ExitGameDlg::onWake(%this) {
	%info = getMissionInfo($Client::MissionFile);

	%title = "<shadow:1:1><shadowcolor:000000bf><just:center>" @ shrinkToFit(%info.name, $DefaultFont["Bold"], 42, 24, getWord(ExitGameInfo.getExtent(), 0));
	ExitGameInfoTitle.setText(%title);
	%text = "<shadow:1:1><shadowcolor:000000bf><font:32>";

	%showAwesome = ((Unlock::getMissionCompletion(%info) & $Completion::Awesome) == $Completion::Awesome) || $Unlock::DisplayAll;

	//Challenge times
	%parTimeLabel      = (%info.time         ? removeLeadingZerosFromTime(formatTime(%info.time))         : "N/A");
	%goldTimeLabel     = (%info.goldTime     ? removeLeadingZerosFromTime(formatTime(%info.goldTime))     : "N/A");
	%platinumTimeLabel = (%info.platinumTime ? removeLeadingZerosFromTime(formatTime(%info.platinumTime)) : "N/A");
	%ultimateTimeLabel = (%info.ultimateTime ? removeLeadingZerosFromTime(formatTime(%info.ultimateTime)) : "N/A");
	%awesomeTimeLabel  = (%info.awesomeTime  ? removeLeadingZerosFromTime(formatTime(%info.awesomeTime))  : "N/A");
	//Challenge scores
	%parScoreLabel      = (%info.score         !$= "" ? formatScore(%info.score)         : "N/A");
	%goldScoreLabel     = (%info.goldScore     !$= "" ? formatScore(%info.goldScore)     : "N/A");
	%platinumScoreLabel = (%info.platinumScore !$= "" ? formatScore(%info.platinumScore) : "N/A");
	%ultimateScoreLabel = (%info.ultimateScore !$= "" ? formatScore(%info.ultimateScore) : "N/A");
	%awesomeScoreLabel  = (%info.awesomeScore  !$= "" ? formatScore(%info.awesomeScore)  : "N/A");

	if (mp()) {
		//See if we have more than one other player
		%vs = !$Server::Hosting //Not host, so there must be someone else who is
			|| (!$Server::_Dedicated && ClientGroup.getCount() > 1) //Hosting local, another player
			|| ($Server::_Dedicated && isObject(ScoreList.player[1])); //Hosting dedicated, hack but should work

		%index = (%vs ? 0 : 1);

		//The MP indexed scores override any previously set scores. But don't override
		// unless we actually have one.
		%parScoreLabel      = (%info.score[%index]         !$= "" ? formatScore(%info.score[%index])         : %parScoreLabel);
		%platinumScoreLabel = (%info.platinumScore[%index] !$= "" ? formatScore(%info.platinumScore[%index]) : %platinumScoreLabel);
		%ultimateScoreLabel = (%info.ultimateScore[%index] !$= "" ? formatScore(%info.ultimateScore[%index]) : %ultimateScoreLabel);
		%awesomeScoreLabel  = (%info.awesomeScore[%index]  !$= "" ? formatScore(%info.awesomeScore[%index])  : %awesomeScoreLabel);
	}
	//Use score if a time isn't available
	%parLabel      = (%parScoreLabel     $= "N/A" ? %parTimeLabel       : %parScoreLabel);
	%goldLabel     = (%goldTimeLabel     $= "N/A" ? %goldScoreLabel     : %goldTimeLabel);
	%platinumLabel = (%platinumTimeLabel $= "N/A" ? %platinumScoreLabel : %platinumTimeLabel);
	%ultimateLabel = (%ultimateTimeLabel $= "N/A" ? %ultimateScoreLabel : %ultimateTimeLabel);
	%awesomeLabel  = (%awesomeTimeLabel  $= "N/A" ? %awesomeScoreLabel  : %awesomeTimeLabel);
	%parType      = (%parScoreLabel     $= "N/A" ? "Time" : "Score");
	%goldType     = (%goldTimeLabel     $= "N/A" ? "Score" : "Time");
	%platinumType = (%platinumTimeLabel $= "N/A" ? "Score" : "Time");
	%ultimateType = (%ultimateTimeLabel $= "N/A" ? "Score" : "Time");
	%awesomeType  = (%awesomeTimeLabel  $= "N/A" ? "Score" : "Time");

	//Figure out what times/scores it needs
	%defined = "Gold Platinum Ultra PlatinumQuest LBCustom";
	%game = ((findWord(%defined, $CurrentGame) == -1) ? resolveMissionModification(%info) : $CurrentGame);
	switch$ (%game) {
	case "Gold":
		//Need qualify and gold times
		%parTitle = "Qualify";
		%goldTitle = "<shadow:1:1><shadowcolor:000000bf><color:FFFF00>Gold";
		%platinumTitle = "<shadow:1:1><shadowcolor:000000bf><color:CCCCCC>Platinum"; // For hunt maps, just in case
		%ultimateTitle = "<shadow:1:1><shadowcolor:000000bf><color:FFCC33>Ultimate"; // For hunt maps, just in case
		%awesomeTitle = "";
	case "Platinum":
		//Need par / platinum / ultimate
		%parTitle = "Par";
		%goldTitle = "<shadow:1:1><shadowcolor:000000bf><color:CCCCCC>Platinum";
		%platinumTitle = "<shadow:1:1><shadowcolor:000000bf><color:CCCCCC>Platinum"; // For hunt maps, just in case
		%ultimateTitle = "<shadow:1:1><shadowcolor:000000bf><color:FFCC33>Ultimate";
		%awesomeTitle = "";
	case "Ultra":
		//Need par / gold / ultimate
		%parTitle = "Par";
		%goldTitle = "<shadow:1:1><shadowcolor:000000bf><color:FFFF00>Gold";
		%platinumTitle = "<shadow:1:1><shadowcolor:000000bf><color:CCCCCC>Platinum"; // For hunt maps, just in case
		%ultimateTitle = "<shadow:1:1><shadowcolor:000000bf><color:FFCC33>Ultimate";
		%awesomeTitle = "";
	case "PlatinumQuest":
		//Need par / platinum / ultimate / (awesome?)
		%parTitle = "Par";
		%goldTitle = "";
		%platinumTitle = "<shadow:1:1><shadowcolor:000000bf><color:CCCCCC>Platinum";
		%ultimateTitle = "<shadow:1:1><shadowcolor:000000bf><color:FFCC33>Ultimate";
		%awesomeTitle = "<shadow:1:1><shadowcolor:000000bf><color:FF4444>Awesome";
	case "LBCustom":
		//Not really sure with these, just assume platinum
		%parTitle = "Par";
		%goldTitle = "<shadow:1:1><shadowcolor:000000bf><color:CCCCCC>Platinum";
		%platinumTitle = "<shadow:1:1><shadowcolor:000000bf><color:CCCCCC>Platinum"; // For hunt maps, just in case
		%ultimateTitle = "<shadow:1:1><shadowcolor:000000bf><color:FFCC33>Ultimate";
		%awesomeTitle = "<shadow:1:1><shadowcolor:000000bf><color:FF4444>Awesome";
	default:
		//It's custom, we're not really sure here
		%parTitle = "Par";
		%goldTitle = "<shadow:1:1><shadowcolor:000000bf><color:FFFF00>Gold";
		%platinumTitle = "<shadow:1:1><shadowcolor:000000bf><color:CCCCCC>Platinum";
		%ultimateTitle = "<shadow:1:1><shadowcolor:000000bf><color:FFCC33>Ultimate";
		%awesomeTitle = "<shadow:1:1><shadowcolor:000000bf><color:FF4444>Awesome";
	}

	%recordTitle   = "<shadow:1:1><shadowcolor:000000bf><color:0060f0>Record";
	%eggRecordTitle = "<shadow:1:1><shadowcolor:000000bf>" @ %eggThemeColor @ "Egg Record";

	//Get the world record
	%record = -1;
	if (lb()) {
		
		%cache = PlayMissionGui.globalScoreCache[PlayMissionGui.getMissionInfo().id];
		if (isObject(%cache)) {
			%scores = %cache.scores;
			if (%scores.getSize()) {
				%record = %scores.getEntry(0).score;
				%recordType = %scores.getEntry(0).score_type;
				%recordLabel = (%recordType $= "time" ? removeLeadingZerosFromTime(formatTime(%record)) : formatScore(%record));
			}
		}

		%cache = PlayMissionGui.globalScoreModeCache[PlayMissionGui.getMissionInfo().id]; 
		if (isObject(%cache)) {
			%scores = %cache.getFieldValue("egg").scores; // Egg scores
			if (%scores.getSize()) {
				%eggRecordLabel = removeLeadingZerosFromTime(formatTime(%scores.getEntry(0).time));
			}
		}
	}
	%challengeTitles = "";
	%challengeTimes = "";

	%info = PlayMissionGui.getMissionInfo();
	%pbType = getField($hs[0], 0);
	%pbLabel = %pbType == 0 ? removeLeadingZerosFromTime(formatTime(getField($hs[0], 1))) : formatScore(getField($hs[0], 1));
	%pbColor = getScoreFormatting($hs[0], %info, "", (%record == getField($hs[0], 1))? 1 : 2); // If record is unset, it would be -1 and never trigger.
	%pbEggLabel = removeLeadingZerosFromTime(formatTime(getField($hs["egg"], 0)));

	%eggThemeColor = (%game $= "PlatinumQuest") ? "<color:cccc99>" : "<color:4580ff>";

	%pbTitle = "<shadow:1:1><shadowcolor:000000bf>Personal Best";
	%pbEggTitle = "<shadow:1:1><shadowcolor:000000bf>" @ %eggThemeColor @ "Egg Best";

	if (getField($hs[0], 2) $= "Matan W." && (%pbType == 0 && getSubStr(%pbLabel, 0, 5) $= "99:59") || (%pbType == 1 && getField($hs[0], 1) == 0)) {
		ExitGameInfoPersonalBestTitle.setVisible(false);
		ExitGameInfoPersonalBestTime.setVisible(false);
	} else {
		ExitGameInfoPersonalBestTitle.setText("<just:center><font:24>" @ %pbColor @ %pbTitle);
		ExitGameInfoPersonalBestTime.setText("<just:center><font:32>" @ %pbLabel);
		ExitGameInfoPersonalBestTitle.setVisible(true);
		ExitGameInfoPersonalBestTime.setVisible(true);
	}


	if ($Game::GotEggThisSession) {
		ExitGameInfoPersonalBestModeTitle.setText("<just:center><font:24>" @ %pbEggTitle);
		ExitGameInfoPersonalBestModeTime.setText("<just:center><font:32>" @ %pbEggLabel);
		ExitGameInfoPersonalBestModeTitle.setVisible(true);
		ExitGameInfoPersonalBestModeTime.setVisible(true);
		if (lb()) {
			ExitGameInfoRecordModeTitle.setText("<just:center><font:24>" @ %eggThemeColor @ %eggRecordTitle);
			ExitGameInfoRecordModeTime.setText("<just:center><font:32>" @ %eggRecordLabel);
			ExitGameInfoRecordModeTitle.setVisible(true);
			ExitGameInfoRecordModeTime.setVisible(true);
		} else {
			ExitGameInfoRecordModeTitle.setVisible(false);
			ExitGameInfoRecordModeTime.setVisible(false);
		}

	} else {
		ExitGameInfoPersonalBestModeTitle.setVisible(false);
		ExitGameInfoPersonalBestModeTime.setVisible(false);
		ExitGameInfoRecordModeTitle.setVisible(false);
		ExitGameInfoRecordModeTime.setVisible(false);
	}

	//Show what we need to
	if (ClientMode::callbackForMission(%info, "timeMultiplier", 1) > 0 && (%parTimeLabel !$= "N/A")) {	//Par can have both time and score
		%challengeTitles = %challengeTitles SPC "<just:center><font:24>" @ %parTitle;
		%challengeTimes  = %challengeTimes  SPC "<just:center><font:32>" @ %parTimeLabel;
	} else if (%parScoreLabel !$= "N/A") { // It shouldn't be possible to have both at once though
		%challengeTitles = %challengeTitles SPC "<just:center><font:24>" @ %parTitle;
		%challengeTimes  = %challengeTimes  SPC "<just:center><font:32>" @ %parScoreLabel;
	}

	if (%goldTitle     !$= "" && %goldLabel     !$= "N/A") {
		%challengeTitles = %challengeTitles SPC "<just:center><font:24>" @ %goldTitle;
		%challengeTimes  = %challengeTimes  SPC "<just:center><font:32>" @ %goldLabel;
	}
	if (%platinumTitle !$= "" && %platinumLabel !$= "N/A") {
		%challengeTitles = %challengeTitles SPC "<just:center><font:24>" @ %platinumTitle;
		%challengeTimes  = %challengeTimes  SPC "<just:center><font:32>" @ %platinumLabel;
	}
	if (%ultimateTitle !$= "" && %ultimateLabel !$= "N/A") {
		%challengeTitles = %challengeTitles SPC "<just:center><font:24>" @ %ultimateTitle;
		%challengeTimes  = %challengeTimes  SPC "<just:center><font:32>" @ %ultimateLabel;
	}
	if (%showAwesome && %awesomeTitle !$= "" && %awesomeLabel !$= "N/A") {
		%challengeTitles = %challengeTitles SPC "<just:center><font:24>" @ %awesomeTitle;
		%challengeTimes  = %challengeTimes  SPC "<just:center><font:32>" @ %awesomeLabel;
	}

	if (lb() && (%showAwesome || $LBPref::ShowRecords) && (%recordLabel !$= "")) {
		%challengeTitles = %challengeTitles SPC "<just:center><font:24>" @ %recordTitle;
		%challengeTimes  = %challengeTimes  SPC "<just:center><font:32>" @ %recordLabel;
	}
	%challengeTitles = ltrim(%challengeTitles); // trim the first space
	%challengeTimes = ltrim(%challengeTimes);
	%challengeLength = getWordCount(%challengeTitles);

	// The display is size 1 through 4, and there are five unique positionings (left, l-center, center, r-center, right). Get rid of some of these times if our PB is higher.
	// ----- 1 -----
	// --- 2 - 3 ---
	// 1 --- 2 --- 3
	// 1 - 2 - 3 - 4


	if (%challengeLength == 1) {
		ExitGameInfoCenterChallengeTitle.setText(%challengeTitles);
		ExitGameInfoCenterChallengeTime.setText(%challengeTimes);
	} else if (%challengeLength == 2) {
		ExitGameInfoLeftcenterChallengeTitle.setText(getWord(%challengeTitles, 0));
		ExitGameInfoLeftcenterChallengeTime.setText(getWord(%challengeTimes, 0));
		ExitGameInfoRightcenterChallengeTitle.setText(getWord(%challengeTitles, 1));
		ExitGameInfoRightcenterChallengeTime.setText(getWord(%challengeTimes, 1));
	} else if (%challengeLength == 3) {
		ExitGameInfoLeftmostChallengeTitle.setText(getWord(%challengeTitles, 0));
		ExitGameInfoLeftmostChallengeTime.setText(getWord(%challengeTimes, 0));
		ExitGameInfoCenterChallengeTitle.setText(getWord(%challengeTitles, 1));
		ExitGameInfoCenterChallengeTime.setText(getWord(%challengeTimes, 1));
		ExitGameInfoRightmostChallengeTitle.setText(getWord(%challengeTitles, 2));
		ExitGameInfoRightmostChallengeTime.setText(getWord(%challengeTimes, 2));
	} else if (%challengeLength == 4) {
		ExitGameInfoLeftmostChallengeTitle.setText(getWord(%challengeTitles, 0));
		ExitGameInfoLeftmostChallengeTime.setText(getWord(%challengeTimes, 0));
		ExitGameInfoLeftcenterChallengeTitle.setText(getWord(%challengeTitles, 1));
		ExitGameInfoLeftcenterChallengeTime.setText(getWord(%challengeTimes, 1));
		ExitGameInfoRightcenterChallengeTitle.setText(getWord(%challengeTitles, 2));
		ExitGameInfoRightcenterChallengeTime.setText(getWord(%challengeTimes, 2));
		ExitGameInfoRightmostChallengeTitle.setText(getWord(%challengeTitles, 3));
		ExitGameInfoRightmostChallengeTime.setText(getWord(%challengeTimes, 3));
	} else if (%challengeLength >= 5) { // Meaning it's all five, par, platinum, ultimate, awesome, record - prerequisite for this if-condition is %showAwesome, which means the player already beat awesome, so hide platinum. Par could be hidden too, but that one seems more relevant to me.
		ExitGameInfoLeftmostChallengeTitle.setText(getWord(%challengeTitles, 0));
		ExitGameInfoLeftmostChallengeTime.setText(getWord(%challengeTimes, 0));
		ExitGameInfoLeftcenterChallengeTitle.setText(getWord(%challengeTitles, 2));
		ExitGameInfoLeftcenterChallengeTime.setText(getWord(%challengeTimes, 2));
		ExitGameInfoRightcenterChallengeTitle.setText(getWord(%challengeTitles, 3));
		ExitGameInfoRightcenterChallengeTime.setText(getWord(%challengeTimes, 3));
		ExitGameInfoRightmostChallengeTitle.setText(getWord(%challengeTitles, 4));
		ExitGameInfoRightmostChallengeTime.setText(getWord(%challengeTimes, 4));
	}
	ExitGameInfoCenterChallengeTitle.setVisible(%challengeLength == 1 || %challengeLength == 3);
	ExitGameInfoCenterChallengeTime.setVisible(%challengeLength == 1 || %challengeLength == 3);
	ExitGameInfoLeftmostChallengeTitle.setVisible(%challengeLength >= 3);
	ExitGameInfoLeftmostChallengeTime.setVisible(%challengeLength >= 3);
	ExitGameInfoRightmostChallengeTitle.setVisible(%challengeLength >= 3);
	ExitGameInfoRightmostChallengeTime.setVisible(%challengeLength >= 3);
	ExitGameInfoLeftcenterChallengeTitle.setVisible(%challengeLength == 2 || %challengeLength >= 4);
	ExitGameInfoLeftcenterChallengeTime.setVisible(%challengeLength == 2 || %challengeLength >= 4);
	ExitGameInfoRightcenterChallengeTitle.setVisible(%challengeLength == 2 || %challengeLength >= 4);
	ExitGameInfoRightcenterChallengeTime.setVisible(%challengeLength == 2 || %challengeLength >= 4);



	%text = %text NL "";

	//Game modes display
	%modes = resolveMissionGameModes(%info.gameMode);
	%modes = formatGameModes(%modes);
	for (%i = 0; %i < getWordCount(%modes); %i ++) {
		%mode = getWord(%modes, %i);
		%minfo = getModeInfo(%mode);
		%text = %text NL "<bold:22><just:left>" @ %minfo.name @ ": <font:22>" @ %minfo.desc;
	}

	ExitGameInfo.setText(%text);
}

function ExitGameDlg::onSleep(%this) {
	//if ($Game::DoPushMoveMap) {
	//	MoveMap.push();
	//	$Game::DoPushMoveMap = false;
	//}
}

function ExitGameDlg::close(%this) {
	RootGui.popDialog(%this);
}
